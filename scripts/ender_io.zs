# Materials
var logicProcessor              = <appliedenergistics2:item.ItemMultiMaterial:22>;
var calculationProcessor        = <appliedenergistics2:item.ItemMultiMaterial:23>;
var engineeringProcessor        = <appliedenergistics2:item.ItemMultiMaterial:24>;
var manaGlass                   = <Botania:manaGlass>;
var manaSteel                   = <Botania:manaResource>;
var manaPearl                   = <Botania:manaResource:1>;
var manaDiamond                 = <Botania:manaResource:2>;
var terraSteel                  = <Botania:manaResource:4>;
var pixieDust                   = <Botania:manaResource:8>;
var prismarineShard             = <Botania:manaResource:10>;
var manaweaveCloth              = <Botania:manaResource:22>;
var runeOfFire                  = <Botania:rune:1>;
var runeOfLust                  = <Botania:rune:9>;
var runeOfGluttony              = <Botania:rune:10>;
var runeOfGreed                 = <Botania:rune:11>;
var runeOfSloth                 = <Botania:rune:12>;
var runeOfWrath                 = <Botania:rune:13>;
var runeOfEnvy                  = <Botania:rune:14>;
var runeOfPride                 = <Botania:rune:15>;
var alphGlass                   = <Botania:elfGlass>;
var fusedQuartz                 = <EnderIO:blockFusedQuartz>;
var endermanHead                = <EnderIO:blockEndermanSkull>;
var tormentedEndermanHead       = <EnderIO:blockEndermanSkull:2>;
var reinforcedObsidian          = <EnderIO:blockReinforcedObsidian>;
var darkIronBars                = <EnderIO:blockDarkIronBars>;
var electricalSteel             = <EnderIO:itemAlloy>;
var energeticAlloy              = <EnderIO:itemAlloy:1>;
var vibrantAlloy                = <EnderIO:itemAlloy:2>;
var redstoneAlloy               = <EnderIO:itemAlloy:3>;
var conductiveIron              = <EnderIO:itemAlloy:4>;
var pulsatingIron               = <EnderIO:itemAlloy:5>;
var darksteelIngot              = <EnderIO:itemAlloy:6>;
var soulariumIngot              = <EnderIO:itemAlloy:7>;
var soulariumBlock              = <EnderIO:blockIngotStorage:7>;
var basicItemFilter             = <EnderIO:itemBasicFilterUpgrade>;
var advancedItemFilter          = <EnderIO:itemBasicFilterUpgrade:1>;
var existingItemFilter          = <EnderIO:itemExistingItemFilter>;
var modItemFilter               = <EnderIO:itemModItemFilter>;
var chargeableItemFilter        = <EnderIO:itemPowerItemFilter>;
var speedUpgrade                = <EnderIO:itemExtractSpeedUpgrade>;
var speedDowngrade              = <EnderIO:itemExtractSpeedUpgrade:1>;
var basicCapacitor              = <EnderIO:itemBasicCapacitor>;
var doubleLayerCapacitor        = <EnderIO:itemBasicCapacitor:1>;
var octadicCapacitor            = <EnderIO:itemBasicCapacitor:2>;
var zombieElectrode             = <EnderIO:itemFrankenSkull>;
var zLogicController            = <EnderIO:itemFrankenSkull:1>;
var frankNZombie                = <EnderIO:itemFrankenSkull:2>;
var enderResonator              = <EnderIO:itemFrankenSkull:3>;
var sentientEnder               = <EnderIO:itemFrankenSkull:4>;
var itemConduit                 = <EnderIO:itemItemConduit>;
var fluidConduit                = <EnderIO:itemLiquidConduit>;
var pressurizedFluidConduit     = <EnderIO:itemLiquidConduit:1>;
var enderFluidConduit           = <EnderIO:itemLiquidConduit:2>;
var machineChassis              = <EnderIO:itemMachinePart>;
var silicon                     = <EnderIO:itemMaterial>;
var conduitBinder               = <EnderIO:itemMaterial:1>;
var binderComposite             = <EnderIO:itemMaterial:2>;
var pulsatingIronNug            = <EnderIO:itemMaterial:3>;
var vibrantAlloyNug             = <EnderIO:itemMaterial:4>;
var pulsatingCrystal            = <EnderIO:itemMaterial:5>;
var vibrantCrystal              = <EnderIO:itemMaterial:6>;
var enderCrystal                = <EnderIO:itemMaterial:8>;
var enticingCrystal             = <EnderIO:itemMaterial:9>;
var weatherCrystal              = <EnderIO:itemMaterial:10>;
var energyConduit               = <EnderIO:itemPowerConduit>;
var enhancedEnergyConduit       = <EnderIO:itemPowerConduit:1>;
var enderEnergyConduit          = <EnderIO:itemPowerConduit:2>;
var remoteAwarenessUpgrade      = <EnderIO:itemFunctionUpgrade>;
var enderPearlShard             = <GrimoireOfGaia:item.GrimoireOfGaia.Shard:5>;
var comparator                  = <minecraft:comparator>;
var emerald                     = <minecraft:emerald>;
var eyeOfEnder                  = <minecraft:ender_eye>;
var goldIngot                   = <minecraft:gold_ingot>;
var gravel                      = <minecraft:gravel>;
var glowstone                   = <minecraft:glowstone>;
var glowstoneDust               = <minecraft:glowstone_dust>;
var ironIngot                   = <minecraft:iron_ingot>;
var obsidian                    = <minecraft:obsidian>;
var redstone                    = <minecraft:redstone>;
var skeletonHead                = <minecraft:skull>;
var zombieHead                  = <minecraft:skull:2>;
var creeperHead                 = <minecraft:skull:4>;
var clayBall                    = <minecraft:clay_ball>;
var plasticSheet                = <MineFactoryReloaded:plastic.sheet>;
var rubberBar                   = <MineFactoryReloaded:rubber.bar>;
var searedBrick                 = <TConstruct:materials:2>;
var searedBrick2                = <TConstruct:materials:37>;
var steelIngot                  = <TConstruct:materials:16>;
var steelBlock                  = <TConstruct:MetalBlock:9>;
var alumiteRod                  = <TConstruct:toolRod:15>;
var invarToughRod               = <TConstruct:toughRod:1020>;
var silverKnifeBlade            = <TConstruct:knifeBlade:1023>;
var nitor                       = <Thaumcraft:ItemResource:1>;
var enchantedFabric             = <Thaumcraft:ItemResource:7>;
var voidMetalIngot              = <Thaumcraft:ItemResource:16>;
var voidMetalNug                = <Thaumcraft:ItemNugget:7>;
var wardedGlass                 = <Thaumcraft:blockCosmeticOpaque:2>;
var basicMachineFrame           = <ThermalExpansion:Frame>;
var hardenedMachineFrame        = <ThermalExpansion:Frame:1>;
var reinforcedMachineFrame      = <ThermalExpansion:Frame:2>;
var resonantMachineFrame        = <ThermalExpansion:Frame:3>;
var tesseractFrame              = <ThermalExpansion:Frame:11>;
var ironGear                    = <ThermalFoundation:material:12>;
var copperGear                  = <ThermalFoundation:material:128>;
var tinGear                     = <ThermalFoundation:material:129>;
var silverGear                  = <ThermalFoundation:material:130>;
var invarGear                   = <ThermalFoundation:material:136>;
var enderiumGear                = <ThermalFoundation:material:140>;
var telluraTumultuous           = <ProjRed|Core:projectred.core.part:10>;
var telluraDeliquescent         = <ProjRed|Core:projectred.core.part:55>;
var telluraResolute             = <magicalcrops:magicalcrops_EssenceIngots>;
var telluraEphemeral            = <magicalcrops:magicalcrops_EssenceIngots:1>;

# Devices
var hopperhockFloating          = <Botania:floatingSpecialFlower>.withTag({type: "hopperhock"});
var chest                       = <minecraft:chest>;
var redstoneComparator          = <minecraft:comparator>;
var piston                      = <minecraft:piston>;
var furnaceMinecraft            = <minecraft:furnace>;
var ironPressurePlate           = <minecraft:heavy_weighted_pressure_plate>;
var tank                        = <OpenBlocks:tank>;
var darkPressurePlate           = <EnderIO:blockDarkSteelPressurePlate>;
var stirlingGenerator           = <EnderIO:blockStirlingGenerator>;
var sagMill                     = <EnderIO:blockSagMill>;
var alloySmelter                = <EnderIO:blockAlloySmelter>;
var photovoltaicCell            = <EnderIO:blockSolarPanel>;
var advancedPhotovoltaicCell    = <EnderIO:blockSolarPanel:1>;
var zombieGenerator             = <EnderIO:blockZombieGenerator>;
var combustionGenerator         = <EnderIO:blockCombustionGenerator>;
var fluidTank                   = <EnderIO:blockTank>;
var pressurizedFluidTank        = <EnderIO:blockTank:1>;
var painter                     = <EnderIO:blockPainter>;
var crafter                     = <EnderIO:blockCrafter>;
var theVat                      = <EnderIO:blockVat>;
var powerMonitor                = <EnderIO:blockPowerMonitor>;
var farmingStation              = <EnderIO:blockFarmStation>;
var wirelessCharger             = <EnderIO:blockWirelessCharger>;
var reservoir                   = <EnderIO:blockReservoir>;
var vacuumChest                 = <EnderIO:blockVacuumChest>;
var dimensionalTransceirver     = <EnderIO:blockTransceiver>;
var itemBuffer                  = <EnderIO:blockBuffer>;
var powerBuffer                 = <EnderIO:blockBuffer:1>;
var omniBuffer                  = <EnderIO:blockBuffer:2>;
var enderIO                     = <EnderIO:blockEnderIo>;
var travelAnchor                = <EnderIO:blockTravelAnchor>;
var sliceNSplice                = <EnderIO:blockSliceAndSplice>;
var soulBinder                  = <EnderIO:blockSoulBinder>;
var poweredSpawner              = <EnderIO:blockPoweredSpawner>;
var killerJoe                   = <EnderIO:blockKillerJoe>;
var attractorObelisk            = <EnderIO:blockAttractor>;
var aversionObelisk             = <EnderIO:blockSpawnGuard>;
var experienceObelisk           = <EnderIO:blockExperienceObelisk>;
var enchanter                   = <EnderIO:blockEnchanter>;
var poweredLight                = <EnderIO:blockElectricLight>;
var unpoweredLight              = <EnderIO:blockElectricLight:2>;
var wirelessPoweredLight        = <EnderIO:blockElectricLight:4>;
var inventoryPanel              = <EnderIO:blockInventoryPanel>;
var telePad                     = <EnderIO:blockTelePad>;
var weatherObelisk              = <EnderIO:blockWeatherObelisk>;
var redstoneFurnace             = <ThermalExpansion:Machine>;
var redstoneFurnace0            = redstoneFurnace.withTag({Level: 0 as byte});
var capacitorBankBasic          = <EnderIO:blockCapBank:1>;
var capacitorBank               = <EnderIO:blockCapBank:2>;
var capacitorBankVibrant        = <EnderIO:blockCapBank:3>;

# Equipment
var conduitProbe                = <EnderIO:itemConduitProbe>;
var bucket                      = <minecraft:bucket>;
var yetaWrench                  = <EnderIO:itemYetaWrench>;
var staffOfTravel               = <EnderIO:itemTravelStaff>;
var gliderWing                  = <EnderIO:itemGliderWing>;
var gliderWings                 = <EnderIO:itemGliderWing:1>;
var soulVial                    = <EnderIO:itemSoulVessel>;
var experienceRod               = <EnderIO:itemXpTransfer>;
var hangGlider                  = <OpenBlocks:hangglider>;
var thaumiumHelm                = <Thaumcraft:ItemHelmetThaumium>;
var thaumiumChest               = <Thaumcraft:ItemChestplateThaumium>;
var thaumiumLeggings            = <Thaumcraft:ItemLeggingsThaumium>;
var thaumiumBoots               = <Thaumcraft:ItemBootsThaumium>;
var darksteelHelm               = <EnderIO:item.darkSteel_helmet>;
var darksteelChest              = <EnderIO:item.darkSteel_chestplate>;
var darksteelLeggings           = <EnderIO:item.darkSteel_leggings>;
var darksteelBoots              = <EnderIO:item.darkSteel_boots>;

# Ore Dictionary Items
var allSand                     = <ore:sand>;
var allGlassBlock               = <ore:blockGlass>;
var allGlassPane                = <ore:paneGlass>;
var allCopperIngot              = <ore:ingotCopper>;
var allTinIngot                 = <ore:ingotTin>;
var allCopperNugs               = <ore:nuggetCopper>;
var allSilverNug                = <ore:nuggetSilver>;
var allGoldNugs                 = <ore:nuggetGold>;
var allTinNug                   = <ore:nuggetTin>;
var allQuartz                   = <ore:anyQuartz>;
var allFluixCrystal             = <ore:anyFluixCrystal>;

################################################################
### Removal ####################################################
################################################################
# Tanks
recipes.remove(fluidTank);
recipes.remove(pressurizedFluidTank);

# Crafter
recipes.remove(crafter);

# Experience Obelisk
recipes.remove(experienceObelisk);

# Enchanter
recipes.remove(enchanter);

# Weather Obelisk
recipes.remove(weatherObelisk);

# Weather Crystal
recipes.remove(weatherCrystal);

# Experience Rod
recipes.remove(experienceRod);

################################################################
### STONE TIER #################################################
################################################################
# Binder Composite
recipes.remove(binderComposite);
recipes.addShaped(binderComposite * 16, [
    [gravel,    gravel,     gravel],
    [allSand,   clayBall,   allSand],
    [gravel,    gravel,     gravel]
]);

# Unpowered Light
recipes.remove(unpoweredLight);
recipes.addShaped(unpoweredLight * 8, [
    [null,          null,           null],
    [allGlassPane,  allGlassPane,   allGlassPane],
    [ironIngot,     glowstone,      ironIngot]
]);

# Conduit Binde
furnace.remove(conduitBinder);
furnace.addRecipe(conduitBinder * 8, binderComposite);

################################################################
### TINKER TIER ################################################
################################################################
# Basic Capacitor
recipes.remove(basicCapacitor);
recipes.addShaped(basicCapacitor, [
    [null,          redstone,   null],
    [allGoldNugs,   rubberBar,  allGoldNugs],
    [null,          redstone,   null]
]);
recipes.addShaped(basicCapacitor, [
    [null,      allGoldNugs,    null],
    [redstone,  rubberBar,      redstone],
    [null,      allGoldNugs,    null]
]);

# Fluid Conduit
recipes.remove(fluidConduit);
recipes.addShaped(fluidConduit * 16, [
    [conduitBinder, conduitBinder,  conduitBinder],
    [allGlassBlock, allGlassBlock,  allGlassBlock],
    [conduitBinder, conduitBinder,  conduitBinder]
]);

# Fluid Conduit
recipes.remove(yetaWrench);
recipes.addShaped(yetaWrench, [
    [ironIngot,     null,       ironIngot],
    [null,          ironGear,   null],
    [null,          rubberBar,  null]
]);

################################################################
### ALCHEMY TIER ###############################################
################################################################
# Tier Implies:
#   Energy Conduit
#   Basic Capacitor Bank

# Machine Chassis (Used by AE2 Machines)
machineChassis.displayName = "Vibration Chassis";
recipes.remove(machineChassis);
recipes.addShaped(machineChassis, [
    [ironIngot, allFluixCrystal,    ironIngot],
    [allQuartz, nitor,              allQuartz],
    [ironIngot, allFluixCrystal,    ironIngot]
]);
recipes.addShaped(machineChassis, [
    [ironIngot,         allQuartz,  ironIngot],
    [allFluixCrystal,   nitor,      allFluixCrystal],
    [ironIngot,         allQuartz,  ironIngot]
]);

# Double Layer Capacitor
recipes.remove(doubleLayerCapacitor);
recipes.addShaped(doubleLayerCapacitor, [
    [null,              basicCapacitor,     null],
    [conductiveIron,    prismarineShard,    conductiveIron],
    [null,              basicCapacitor,     null]
]);
recipes.addShaped(doubleLayerCapacitor, [
    [null,              conductiveIron,     null],
    [basicCapacitor,    prismarineShard,    basicCapacitor],
    [null,              conductiveIron,     null]
]);

# Capacitor Bank
recipes.remove(capacitorBank);
recipes.addShaped(capacitorBank, [
    [steelIngot,            doubleLayerCapacitor,               steelIngot],
    [doubleLayerCapacitor,  capacitorBankBasic.marked("inp"),   doubleLayerCapacitor],
    [steelIngot,            doubleLayerCapacitor,               steelIngot]],
    function(output, inputs, crafting) {
        return output.withTag({type: "ACTIVATED", storedEnergyRF: inputs.inp.tag.storedEnergyRF});
    }
);

# Stirling Generator
recipes.remove(stirlingGenerator);
recipes.addShaped(stirlingGenerator, [
    [searedBrick,   furnaceMinecraft,   searedBrick],
    [searedBrick,   nitor,              searedBrick],
    [ironGear,      piston,             ironGear]
]);

# Photovoltaic Cell
recipes.remove(photovoltaicCell);
recipes.addShaped(photovoltaicCell, [
    [allGlassPane,      allGlassPane,   allGlassPane],
    [silicon,           silicon,        silicon],
    [conductiveIron,    basicCapacitor, conductiveIron]
]);

# Powered Light
recipes.remove(poweredLight);
recipes.addShaped(poweredLight * 8, [
    [allGlassPane,  allGlassPane,   allGlassPane],
    [silicon,       glowstoneDust,  silicon],
    [ironIngot,     basicCapacitor, ironIngot]
]);

# SAG Mill
recipes.remove(sagMill);
recipes.addShaped(sagMill, [
    [ironIngot,    invarGear,          ironIngot],
    [invarGear,    null,               invarGear],
    [ironIngot,    basicMachineFrame,  ironIngot]
]);

# Painter
recipes.remove(painter);
recipes.addShaped(painter, [
    [allTinIngot,   null,               allTinIngot],
    [allTinIngot,   fluidConduit,       allTinIngot],
    [tinGear,       basicMachineFrame,  tinGear]
]);

# Power Monitor
recipes.remove(powerMonitor);
recipes.addShaped(powerMonitor, [
    [allTinIngot,   conduitProbe,   allTinIngot],
    [allGlassPane,  allGlassPane,   allGlassPane],
    [allTinIngot,   energyConduit,  allTinIngot]
]);

# Vacuum Chest
recipes.remove(vacuumChest);
recipes.addShaped(vacuumChest, [
    [allTinIngot,   tinGear,            allTinIngot],
    [tinGear,       hopperhockFloating, tinGear],
    [allTinIngot,   tinGear,            allTinIngot]
]);

# Reservoir
recipes.remove(reservoir);
mods.thaumcraft.Arcane.addShaped(
    "ASPECTS",
    reservoir,
    "aqua 2", [
    [allGlassBlock, allGlassBlock,  allGlassBlock],
    [allGlassBlock, allSilverNug,   allGlassBlock],
    [allGlassBlock, allGlassBlock,  allGlassBlock]
]);

# Pressurized Fluid Conduit
recipes.remove(pressurizedFluidConduit);
recipes.addShaped(pressurizedFluidConduit * 16, [
    [conduitBinder, conduitBinder,  conduitBinder],
    [manaGlass,     manaGlass,      manaGlass],
    [conduitBinder, conduitBinder,  conduitBinder]
]);

# The Vat
recipes.remove(theVat);
recipes.addShaped(theVat, [
    [searedBrick2,  searedBrick2,           searedBrick2],
    [tank,          bucket,                 tank],
    [copperGear,    hardenedMachineFrame,   copperGear]
]);

# Combustion Generator
recipes.remove(combustionGenerator);
recipes.addShaped(combustionGenerator, [
    [steelIngot,    doubleLayerCapacitor,   steelIngot],
    [invarGear,     steelBlock,             invarGear],
    [invarGear,     invarToughRod,          invarGear]
]);
recipes.addShaped(combustionGenerator, [
    [steelIngot,    doubleLayerCapacitor,   steelIngot],
    [invarGear,     stirlingGenerator,      invarGear],
    [invarGear,     null,                   invarGear]
]);

# Inventory Panel
recipes.remove(inventoryPanel);
recipes.addShaped(inventoryPanel, [
    [remoteAwarenessUpgrade,    calculationProcessor,   remoteAwarenessUpgrade],
    [itemConduit,               zombieHead,             itemConduit],
    [tinGear,                   tank,   tinGear]
]);

# Item Buffer
recipes.remove(itemBuffer);
recipes.addShaped(itemBuffer, [
    [allTinNug, itemConduit,    allTinNug],
    [allTinNug, chest,          allTinNug],
    [allTinNug, tinGear,        allTinNug]
]);

# Power Buffer
recipes.remove(powerBuffer);
recipes.addShaped(powerBuffer, [
    [allTinNug, conductiveIron, allTinNug],
    [allTinNug, basicCapacitor, allTinNug],
    [allTinNug, conductiveIron, allTinNug]
]);

# Conductive Iron
mods.thaumcraft.Crucible.addRecipe(
    "CRUCIBLE",
    conductiveIron,
    ironIngot,
    "potentia 2, machina 1"
);

# Redstone Alloy
mods.thaumcraft.Crucible.addRecipe(
    "CRUCIBLE",
    redstoneAlloy,
    silicon,
    "potentia 2, machina 1"
);

# Basic Item Filter
recipes.remove(basicItemFilter);
recipes.addShaped(basicItemFilter, [
    [null,  null,       null],
    [null,  tinGear,    null],
    [null,  silicon,    null]
]);

# Advanced Item Filter
recipes.remove(advancedItemFilter);
recipes.addShaped(advancedItemFilter, [
    [null,  null,                   null],
    [null,  calculationProcessor,   null],
    [null,  basicItemFilter,        null]
]);

# Mod Item Filter
recipes.remove(modItemFilter);
recipes.addShaped(modItemFilter, [
    [null,  null,               null],
    [null,  comparator,         null],
    [null,  advancedItemFilter, null]
]);

# Chargeable Item Filter
recipes.remove(chargeableItemFilter);
recipes.addShaped(chargeableItemFilter, [
    [null,  null,               null],
    [null,  conduitProbe,       null],
    [null,  basicItemFilter,    null]
]);

# Counduit Speed Downgrade
recipes.remove(speedDowngrade);
recipes.addShaped(speedDowngrade, [
    [null,          conduitBinder,  null],
    [conduitBinder, rubberBar,      conduitBinder],
    [null,          conduitBinder,  null]
]);

# Conduit Probe
recipes.remove(conduitProbe);
recipes.addShaped(conduitProbe, [
    [energyConduit, conduitBinder,      energyConduit],
    [silicon,       allGlassPane,       silicon],
    [conduitBinder, redstoneComparator, conduitBinder]
]);

# Item Conduit (Reduced Count)
recipes.remove(itemConduit);
recipes.addShaped(itemConduit * 4, [
    [conduitBinder,     conduitBinder,      conduitBinder],
    [enderPearlShard,   enderPearlShard,    enderPearlShard],
    [conduitBinder,     conduitBinder,      conduitBinder]
]);


################################################################
### INFUSTION TIER #############################################
################################################################
# Tier Implies:
#   Enhanced Energy Conduit

# Alloy Smelter
recipes.remove(alloySmelter);
recipes.addShaped(alloySmelter, [
    [searedBrick2,  searedBrick2,           searedBrick2],
    [runeOfPride,   redstoneFurnace0,       runeOfEnvy],
    [steelIngot,    reinforcedMachineFrame, steelIngot]
]);
recipes.addShaped(alloySmelter, [
    [searedBrick2,  searedBrick2,           searedBrick2],
    [runeOfEnvy,    redstoneFurnace0,       runeOfPride],
    [steelIngot,    reinforcedMachineFrame, steelIngot]
]);

# Octadic Capacitor
recipes.remove(octadicCapacitor);
recipes.addShaped(octadicCapacitor, [
    [null,          doubleLayerCapacitor,   null],
    [vibrantAlloy,  plasticSheet,           vibrantAlloy],
    [null,          doubleLayerCapacitor,   null]
]);
recipes.addShaped(octadicCapacitor, [
    [null,                  vibrantAlloy,   null],
    [doubleLayerCapacitor,  plasticSheet,   doubleLayerCapacitor],
    [null,                  vibrantAlloy,   null]
]);

# Vibrant Capacitor Bank
recipes.remove(capacitorBankVibrant);
recipes.addShaped(capacitorBankVibrant, [
    [vibrantAlloy,      octadicCapacitor,               vibrantAlloy],
    [octadicCapacitor,  capacitorBank.marked("inp"),    octadicCapacitor],
    [vibrantAlloy,      octadicCapacitor,               vibrantAlloy]],
    function(output, inputs, crafting) {
        return output.withTag({type: "ACTIVATED", storedEnergyRF: inputs.inp.tag.storedEnergyRF});
    }
);

# Vibrant Crystal
recipes.remove(vibrantCrystal);
mods.thaumcraft.Arcane.addShaped(
    "ASPECTS",
    vibrantCrystal,
    "ignis 10, ordo 10", [
    [vibrantAlloyNug,   vibrantAlloyNug,    vibrantAlloyNug],
    [vibrantAlloyNug,   manaDiamond,        vibrantAlloyNug],
    [vibrantAlloyNug,   vibrantAlloyNug,    vibrantAlloyNug]
]);

# Pulsating Crystal
recipes.remove(pulsatingCrystal);
mods.thaumcraft.Arcane.addShaped(
    "ASPECTS",
    pulsatingCrystal,
    "ordo 10, aer 10", [
    [pulsatingIronNug,  pulsatingIronNug,   pulsatingIronNug],
    [pulsatingIronNug,  manaPearl,          pulsatingIronNug],
    [pulsatingIronNug,  pulsatingIronNug,   pulsatingIronNug]
]);

# Enticing Crystal
recipes.remove(enticingCrystal);
mods.botania.ElvenTrade.addRecipe(enticingCrystal, [emerald]);

# Reinforced Obsidian
recipes.remove(reinforcedObsidian);
recipes.addShaped(reinforcedObsidian * 16, [
    [steelIngot,    darkIronBars,   steelIngot],
    [darkIronBars,  obsidian,       darkIronBars],
    [steelIngot,    darkIronBars,   steelIngot]
]);

# Advanced Photovoltaic Cell
recipes.remove(advancedPhotovoltaicCell);
recipes.addShaped(advancedPhotovoltaicCell, [
    [pulsatingIronNug,  pulsatingIronNug,   pulsatingIronNug],
    [vibrantAlloyNug,   vibrantAlloyNug,    vibrantAlloyNug],
    [energeticAlloy,    photovoltaicCell,   energeticAlloy]
]);

# Wireless Powered Light
recipes.remove(wirelessPoweredLight);
recipes.addShaped(wirelessPoweredLight * 8, [
    [poweredLight,  poweredLight,       poweredLight],
    [poweredLight,  electricalSteel,    poweredLight],
    [poweredLight,  poweredLight,       poweredLight]
]);

# Attractor Obelisk
recipes.remove(attractorObelisk);
recipes.addShaped(attractorObelisk, [
    [null,              enticingCrystal,        null],
    [null,              soulariumIngot,         null],
    [soulariumIngot,    reinforcedMachineFrame, soulariumIngot]
]);

# Aversion Obelisk
recipes.remove(aversionObelisk);
recipes.addShaped(aversionObelisk, [
    [null,              tormentedEndermanHead,  null],
    [null,              soulariumIngot,         null],
    [soulariumIngot,    reinforcedMachineFrame, soulariumIngot]
]);

# Ender IO
recipes.remove(enderIO);
recipes.addShaped(enderIO, [
    [soulariumIngot,    fusedQuartz,    soulariumIngot],
    [fusedQuartz,       eyeOfEnder,     fusedQuartz],
    [soulariumIngot,    fusedQuartz,    soulariumIngot]
]);

# Slice N Splice
recipes.remove(sliceNSplice);
recipes.addShaped(sliceNSplice, [
    [soulariumIngot,    silverGear,             soulariumIngot],
    [copperGear,        silverKnifeBlade,       copperGear],
    [soulariumIngot,    reinforcedMachineFrame, soulariumIngot]
]);

# Farming Station
recipes.remove(farmingStation);
recipes.addShaped(farmingStation, [
    [ironIngot,         ironIngot,              ironIngot],
    [pulsatingCrystal,  enderResonator,         pulsatingCrystal],
    [ironGear,          resonantMachineFrame,   ironGear]
]);

# Glider Wing
recipes.remove(gliderWing);
mods.thaumcraft.Arcane.addShaped(
    "ASPECTS",
    gliderWing,
    "aer 10", [
    [null,          alumiteRod,     plasticSheet],
    [alumiteRod,    plasticSheet,   manaweaveCloth],
    [plasticSheet,  manaweaveCloth, enchantedFabric]
]);

# Glider Wings
recipes.remove(gliderWings);
mods.thaumcraft.Arcane.addShaped(
    "ASPECTS",
    gliderWings,
    "aer 30", [
    [gliderWing,    alumiteRod, gliderWing],
    [null,          null,       null],
    [null,          null,       null]
]);
mods.thaumcraft.Arcane.addShaped(
    "ASPECTS",
    gliderWings,
    "aer 30", [
    [null,          null,       null],
    [gliderWing,    alumiteRod, gliderWing],
    [null,          null,       null]
]);
mods.thaumcraft.Arcane.addShaped(
    "ASPECTS",
    gliderWings,
    "aer 30", [
    [null,          null,       null],
    [null,          null,       null],
    [gliderWing,    alumiteRod, gliderWing]
]);
recipes.addShapeless(gliderWings, [hangGlider]);

# Darksteel Helm
recipes.remove(darksteelHelm);
recipes.addShaped(darksteelHelm, [
    [darksteelIngot,    zLogicController,                       darksteelIngot],
    [darksteelIngot,    thaumiumHelm.anyDamage().marked("inp"), darksteelIngot],
    [null,              null,                                   null]],
    function(output, inputs, crafting) {
        return output.withDamage(max(0, inputs.inp.damage));
    }
);

# Darksteel Chest
recipes.remove(darksteelChest);
recipes.addShaped(darksteelChest, [
    [darksteelIngot,    null,                                       darksteelIngot],
    [darksteelIngot,    thaumiumChest.anyDamage().marked("inp"),    darksteelIngot],
    [invarGear,         darksteelIngot,                             invarGear]],
    function(output, inputs, crafting) {
        return output.withDamage(max(0, inputs.inp.damage));
    }
);

# Darksteel Leggings
recipes.remove(darksteelLeggings);
recipes.addShaped(darksteelLeggings, [
    [darksteelIngot,    thaumiumLeggings.anyDamage().marked("inp"), darksteelIngot],
    [invarGear,         null,                                       invarGear],
    [darksteelIngot,    null,                                       darksteelIngot]],
    function(output, inputs, crafting) {
        return output.withDamage(max(0, inputs.inp.damage));
    }
);

# Darksteel Boots
recipes.remove(darksteelBoots);
recipes.addShaped(darksteelBoots, [
    [darksteelIngot,   thaumiumBoots.anyDamage().marked("inp"),   darksteelIngot],
    [null,              null,                                       null],
    [null,              null,                                       null]],
    function(output, inputs, crafting) {
        return output.withDamage(max(0, inputs.inp.damage));
    }
);

# Existing Item Filter
recipes.remove(existingItemFilter);
recipes.addShaped(existingItemFilter, [
    [null,  null,               null],
    [null,  logicProcessor,     null],
    [null,  advancedItemFilter, null]
]);

# Counduit Speed Upgrade
recipes.remove(speedUpgrade);
recipes.addShaped(speedUpgrade, [
    [null,          conduitBinder,      null],
    [conduitBinder, pulsatingIron,   conduitBinder],
    [null,          conduitBinder,      null]
]);

# Dark Pressure PLate
recipes.remove(darkPressurePlate);
recipes.addShapeless(darkPressurePlate, [ironPressurePlate, darksteelIngot]);

# Ender Fluid Conduit
recipes.remove(enderFluidConduit);
recipes.addShaped(enderFluidConduit * 16, [
    [conduitBinder, conduitBinder,  conduitBinder],
    [fusedQuartz,   vibrantAlloy,   fusedQuartz],
    [conduitBinder, conduitBinder,  conduitBinder]
]);

# Item Conduit (High Count)
recipes.addShaped(itemConduit * 16, [
    [conduitBinder,     conduitBinder,      conduitBinder],
    [pulsatingIronNug,  pulsatingIronNug,   pulsatingIronNug],
    [conduitBinder,     conduitBinder,      conduitBinder]
]);

# Fire Water
#mods.tconstruct.Smeltery.addAlloy(<liquid:rocket_fuel> * 64, [<liquid:fire_water> * 64, <liquid:pyrotheum> * 8]);

################################################################
### VOID TIER ##################################################
################################################################
# Telepad Block
recipes.remove(telePad);
recipes.addShaped(telePad, [
    [plasticSheet,      enderCrystal,           plasticSheet],
    [telluraEphemeral,  travelAnchor,           telluraDeliquescent],
    [darksteelIngot,    resonantMachineFrame,   darksteelIngot]
]);
recipes.addShaped(telePad, [
    [plasticSheet,          enderCrystal,           plasticSheet],
    [telluraDeliquescent,   travelAnchor,           telluraEphemeral],
    [darksteelIngot,        resonantMachineFrame,   darksteelIngot]
]);

# Dimentional Transeiver
recipes.remove(dimensionalTransceirver);
recipes.addShaped(dimensionalTransceirver, [
    [telluraResolute,   enderResonator,     telluraResolute],
    [fusedQuartz,       tesseractFrame,     fusedQuartz],
    [telluraResolute,   octadicCapacitor,   telluraResolute]
]);

# Soul Binder
recipes.remove(soulBinder);
mods.thaumcraft.Infusion.addRecipe(
    "VOIDMETAL",
    resonantMachineFrame,
    [endermanHead, soulariumIngot, voidMetalIngot, zombieHead, soulariumIngot, voidMetalIngot, skeletonHead, soulariumIngot, voidMetalIngot, creeperHead, soulariumIngot, voidMetalIngot],
    "machina 8, vinculum 8, permutatio 8, spiritus 16",
    soulBinder,
    4
);

# Powered Spawner
recipes.remove(poweredSpawner);
recipes.addShaped(poweredSpawner, [
    [steelIngot,        enderResonator,         steelIngot],
    [enderCrystal,      null,                   enderCrystal],
    [telluraTumultuous, resonantMachineFrame,   telluraTumultuous]
]);

# Ender Energy Conduit
recipes.remove(enderEnergyConduit);
recipes.addShaped(enderEnergyConduit * 16, [
    [conduitBinder, conduitBinder,  conduitBinder],
    [vibrantAlloy,  voidMetalIngot, vibrantAlloy],
    [conduitBinder, conduitBinder,  conduitBinder]
]);

# Zombie Generator
recipes.remove(zombieGenerator);
mods.thaumcraft.Infusion.addRecipe(
    "VOIDMETAL",
    zombieElectrode,
    [voidMetalIngot, electricalSteel, fusedQuartz, electricalSteel, voidMetalIngot, electricalSteel, fusedQuartz, electricalSteel, voidMetalIngot, electricalSteel, fusedQuartz, electricalSteel, voidMetalIngot, electricalSteel, fusedQuartz, electricalSteel],
    "machina 8, exanimis 8, fames 64",
    zombieGenerator,
    6
);

# Soul Vial
recipes.remove(soulVial);
mods.thaumcraft.Infusion.addRecipe(
    "VOIDMETAL",
    voidMetalNug,
    [soulariumBlock, wardedGlass, alphGlass, wardedGlass, alphGlass, wardedGlass, alphGlass, wardedGlass],
    "spiritus 8, vinculum 16",
    soulVial,
    6
);

# Nutrient Distilation (Large Batch)
weatherCrystal.displayName = "\u00A7cAmalgam Heart";
weatherCrystal.addTooltip(format.gray("It seems the Amalgam contains some living components. This one wiggles and squirms."));
